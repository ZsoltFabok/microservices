task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
        classpath("org.springframework:springloaded:1.2.6.RELEASE")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    // configuration for the devtools
    configurations {
        dev
        springLoaded
    }
    // Use Spring Boot DevTool only when we run Gradle bootRun task
    bootRun {
        classpath = sourceSets.main.runtimeClasspath + configurations.dev
    }

    // Intellij and gradle use the same output dir
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-jetty")
        // TODO: without this line the authservice does not compile. Why?
        compile("org.springframework.boot:spring-boot-starter-thymeleaf")
        compile("org.springframework.boot:spring-boot-devtools")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}


// for codecov

// needs to be on the all level to make the top level
// codeCoverageReport task to work
allprojects {
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
